import org.springframework.boot.gradle.tasks.bundling.BootJar
import org.springframework.boot.gradle.tasks.bundling.BootWar

buildscript {
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${SPRING_BOOT_VERSION}"
        classpath "io.spring.gradle:dependency-management-plugin:${SPRING_DEPENDENCY_MANAGEMENT_VERSION}"
        classpath "com.github.ben-manes:gradle-versions-plugin:${BEN_MANES_VERSION}"
    }
}

plugins {
    id 'java'
}

allprojects {
    tasks.withType(BootJar).configureEach { enabled = false }

    tasks.withType(BootWar).configureEach { enabled = false }

    tasks.withType(Test).tap { configureEach { enabled = false } }

    if (project == rootProject) {
        tasks.matching { it.name in ['build', 'assemble', 'check', 'jar', 'bootJar', 'bootWar'] }
                .configureEach { enabled = false }
    }
}


subprojects {
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java'

    repositories {
        gradlePluginPortal()
        mavenCentral()
        maven { url = 'https://repo.spring.io/milestone' }
        maven { url = 'https://repo.spring.io/snapshot' }
    }

    group = "${GROUP}"

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${SPRING_BOOT_VERSION}"
        }
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    tasks.withType(JavaCompile).tap {
        configureEach {
            options.encoding = 'UTF-8'
        }
    }

    if (project.name == 'workflow-main') {
        apply plugin: 'war'

        tasks.withType(BootWar).configureEach {
            mainClass = 'com.org.workflow.WorkflowApplication'
            archiveFileName = 'work-flow.war'
            enabled = true
            launchScript()
        }
    }
}